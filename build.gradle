buildscript {

  ext.versions = [
    'kotlin': '1.3.61',
    'junit': '5.5.2',
    'moshi': '1.9.2',
    'okhttp': '3.14.3',
    'retrofit': '2.6.1',
  ]

  ext.deps = [
    'okhttp': "com.squareup.okhttp3:okhttp:${versions.okhttp}",
    'coroutine': 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.2',
    'kotlin': [
      'stdlib': "org.jetbrains.kotlin:kotlin-stdlib-jdk8:${versions.kotlin}"
    ],
    'mockWebServer': "com.squareup.okhttp3:mockwebserver:${versions.okhttp}",
    'junit': [
      'api': "org.junit.jupiter:junit-jupiter-api:${versions.junit}",
      'engine': "org.junit.jupiter:junit-jupiter-engine:${versions.junit}",
    ],
    'simpleXml': 'org.simpleframework:simple-xml:2.7.1',
    'mockito': 'org.mockito:mockito-core:2.28.2',
    'rxjava2': 'io.reactivex.rxjava2:rxjava:2.2.15',
    'truth': 'com.google.truth:truth:1.0'
  ]

  repositories {
    mavenCentral()
  }

  dependencies {
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"
  }
}

allprojects {
  if (JavaVersion.current().isJava8Compatible()) {
    tasks.withType(Javadoc) {
      options.addStringOption('Xdoclint:none', '-quiet')
    }
  }
}

subprojects {
  repositories {
    mavenCentral()
  }

  tasks.withType(Javadoc).all { enabled = false } // FIXME: build fails without this.

  group = GROUP
  version = VERSION_NAME
}
